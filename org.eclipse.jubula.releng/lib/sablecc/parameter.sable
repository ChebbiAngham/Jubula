/* Grammar for Client parameter values. */
Package org.eclipse.jubula.client.core.gen.parser.parameter;
    
Helpers
    literal_symbol = ''';
    alphanumeric = ['0'..'9'] | ['a'..'z'] | ['A'..'Z'] | '_';
    any_char = [0 .. 0xffff];
    not_literal_symbol = [any_char - literal_symbol];
    escapable = ''' | '\' | '=' | '$';

States
    normal,
    variable,
    var_brace,
    reference,
    ref_brace,
    literal;

Tokens
    {normal->literal}
    open_literal = ''';
    
	{normal->reference}
    reference_token = '=';
	{normal}
    escaped_symbol = '\' escapable;
	{variable->var_brace,reference->ref_brace}
    open_brace = '{';
	{variable->normal,var_brace->normal,reference->normal,ref_brace->normal}
    close_brace = '}';
	{normal->variable}
    variable_token = '$';
	{normal}
    alphanumeric = alphanumeric+;
    {variable->normal,var_brace->variable}
    variable_body = alphanumeric+;
    {reference->normal,ref_brace->reference}
    reference_body = alphanumeric+;
	{normal}
    char = any_char;

	{literal}
    literal_body = not_literal_symbol+;

    {literal->normal}
    close_literal = ''';
    

Productions
    parameter = param_token+;
    param_token = {literal}P.literal | 
                  {escape_sequence}escaped_symbol | 
                  {reference}P.reference | 
                  {variable}P.variable | 
                  {alphanumeric}alphanumeric |
                  {any_sequence}char;
    literal = open_literal literal_body? close_literal;
    reference = reference_token open_brace? reference_body? close_brace?;
    variable = variable_token open_brace? variable_body? close_brace?;
